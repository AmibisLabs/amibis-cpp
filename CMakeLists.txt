cmake_minimum_required(VERSION 2.6)
project(OMiSCID)

# Set version number
SET(OMISCID_VERSION 1.6.0)
SET(OMISCID_SO_VERSION 1.6)

# By default, build a release
IF(NOT CMAKE_BUILD_TYPE)
  SET(CMAKE_BUILD_TYPE Release CACHE STRING "Choose the type of build")
ENDIF(NOT CMAKE_BUILD_TYPE)

# Configurable values
SET(DEBUG_CHMEM no CACHE BOOL "Compile Debug version using memory leak detection mode")
SET(DEBUG_THREAD no CACHE BOOL "Compile Debug version with thread debug mode")
SET(DEBUG_TRACE no CACHE BOOL "Compile Debug version with tracing mode on")

# Check the dependencies
SET(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR})
FIND_PACKAGE(Iconv REQUIRED) # For LibXml2, needed on Windows?
FIND_PACKAGE(LibXml2 REQUIRED)
FIND_PACKAGE(Threads REQUIRED)
FIND_PACKAGE(DNS-SD)
FIND_PACKAGE(Avahi-Client)

# Choose the zeroconf implementation
IF(AVAHI-CLIENT_FOUND)
  SET(ZEROCONF_INCLUDE_DIR ${AVAHI-CLIENT_INCLUDE_DIR})
  SET(ZEROCONF_LIBRARIES ${AVAHI-CLIENT_LIBRARIES})
  ADD_DEFINITIONS(-DOMISCID_USE_AVAHI)
  SET(OMISCID_CXX_FLAGS "${OMISCID_CXX_FLAGS} -DOMISCID_USE_AVAHI")
ENDIF(AVAHI-CLIENT_FOUND)
IF(DNS-SD_FOUND)
  SET(ZEROCONF_INCLUDE_DIR ${DNS-SD_INCLUDE_DIR})
  SET(ZEROCONF_LIBRARIES ${DNS-SD_LIBRARIES})
  ADD_DEFINITIONS(-DOMISCID_USE_MDNS)
  SET(OMISCID_CXX_FLAGS "${OMISCID_CXX_FLAGS} -DOMISCID_USE_MDNS")
ENDIF(DNS-SD_FOUND)
IF(NOT ZEROCONF_INCLUDE_DIR)
  MESSAGE(FATAL_ERROR "Could NOT find any supported zeroconf implementation (DNS-SD or Avahi)")
ENDIF(NOT ZEROCONF_INCLUDE_DIR)

# Set common compilation options
SET(CMAKE_CXX_FLAGS "-g -Werror -Wall -pedantic -std=c++98")
# Set the debug flags
SET(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -O0 -fno-inline -DDEBUG")
IF(DEBUG_CHMEM)
  SET(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -DTRACKING_MEMORY_LEAKS")
ENDIF(DEBUG_CHMEM)
IF(DEBUG_THREAD)
  SET(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -DDEBUG_THREAD")
ENDIF(DEBUG_THREAD)
IF(DEBUG_TRACE)
  SET(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -DOMISCID_TRACE_ENABLE")
ENDIF(DEBUG_TRACE)

# Go into the subprojects
ADD_SUBDIRECTORY(System)
ADD_SUBDIRECTORY(Com)
ADD_SUBDIRECTORY(ServiceControl)

